# Tiltfile for development of openTDF backend
# reference https://docs.tilt.dev/api.html
# extensions https://github.com/tilt-dev/tilt-extensions

load("ext://helm_remote", "helm_remote")
load("ext://secret", "secret_from_dict", "secret_yaml_generic")
load("ext://min_tilt_version", "min_tilt_version")

min_tilt_version("0.25")

ALPINE_VERSION = os.environ.get("ALPINE_VERSION", "3.15")
PY_VERSION = os.environ.get("PY_VERSION", "3.10")
KEYCLOAK_BASE_VERSION = str(
    local('cut -d- -f1 < "{}"'.format("containers/keycloak-protocol-mapper/VERSION"))
).strip()

# ghcr.io == GitHub packages. pre-release versions, created from recent green `main` commit
# docker.io == Docker hub. Manually released versions
CONTAINER_REGISTRY = os.environ.get("CONTAINER_REGISTRY", "ghcr.io")


def from_dotenv(path, key):
    # Read a variable from a `.env` file
    return str(local('. "{}" && echo "${}"'.format(path, key))).strip()


config.define_string_list("to-run", args=True)
config.define_string_list("to-edit")
cfg = config.parse()
to_edit = cfg.get("to-edit", [])

groups = {
    "all": [
        "keycloak",
        "opentdf-kas",
        "opentdf-attributes",
        "opentdf-claims",
        "opentdf-entitlements",
        "opentdf-postgresql",
    ],
    "integration-test": [
        "keycloak",
        "keycloak-bootstrap",
        "ingress-nginx-controller",
        "ingress-nginx-admission-create",
        "ingress-nginx-admission-patch",
        "opentdf-attributes",
        "opentdf-claims",
        "opentdf-entitlements",
        "opentdf-kas",
        "opentdf-abacus",
        "opentdf-postgresql",
        "opentdf-xtest",
    ],
}

resources = []

# isCI comes from tests/integration/Tiltfile
isCI = False

for arg in cfg.get("to-run", []):
    if arg == "integration-test":
        isCI = True
    if arg in groups:
        resources += groups[arg]
    else:
        # also support specifying individual services instead of groups, e.g. `tilt up a b d`
        resources.append(arg)

config.set_enabled_resources(resources)

#                                                      .
#                                                    .o8
#   .oooo.o  .ooooo.   .ooooo.  oooo d8b  .ooooo.  .o888oo  .oooo.o
#  d88(  "8 d88' `88b d88' `"Y8 `888""8P d88' `88b   888   d88(  "8
#  `"Y88b.  888ooo888 888        888     888ooo888   888   `"Y88b.
#  o.  )88b 888    .o 888   .o8  888     888    .o   888 . o.  )88b
#  8""888P' `Y8bod8P' `Y8bod8P' d888b    `Y8bod8P'   "888" 8""888P'

local("./scripts/genkeys-if-needed")

all_secrets = {
    v: from_dotenv("./certs/.env", v)
    for v in [
        "CA_CERTIFICATE",
        "EAS_CERTIFICATE",
        "KAS_CERTIFICATE",
        "KAS_EC_SECP256R1_CERTIFICATE",
        "KAS_EC_SECP256R1_PRIVATE_KEY",
        "KAS_PRIVATE_KEY",
    ]
}

if isCI and not os.path.exists(
    "./containers/keycloak-protocol-mapper/keycloak-containers/server/Dockerfile"
):
    local("make keycloak-repo-clone", dir="./containers/keycloak-protocol-mapper")

all_secrets["POSTGRES_PASSWORD"] = "myPostgresPassword"
all_secrets["OIDC_CLIENT_SECRET"] = "myclientsecret"
all_secrets["ca-cert.pem"] = all_secrets["CA_CERTIFICATE"]


def only_secrets_named(*items):
    return {k: all_secrets[k] for k in items}


if isCI:
    k8s_yaml(
        secret_from_dict(
            "attributes-secrets",
            inputs=only_secrets_named("OIDC_CLIENT_SECRET", "POSTGRES_PASSWORD"),
        )
    )
    k8s_yaml(
        secret_from_dict(
            "kas-secrets",
            inputs=only_secrets_named(
                "EAS_CERTIFICATE",
                "KAS_EC_SECP256R1_CERTIFICATE",
                "KAS_CERTIFICATE",
                "KAS_EC_SECP256R1_PRIVATE_KEY",
                "KAS_PRIVATE_KEY",
            ),
        )
    )
    k8s_yaml(
        secret_from_dict(
            "keycloak-bootstrap-secrets",
            inputs=only_secrets_named("OIDC_CLIENT_SECRET"),
        )
    )
    k8s_yaml(
        secret_from_dict(
            "claims-secrets",
            inputs=only_secrets_named(
                "POSTGRES_PASSWORD",
                "EAS_CERTIFICATE",
                "KAS_EC_SECP256R1_CERTIFICATE",
                "KAS_CERTIFICATE",
            ),
        )
    )
    k8s_yaml(
        secret_from_dict(
            "entitlements-secrets",
            inputs={
                "OIDC_CLIENT_SECRET": all_secrets["OIDC_CLIENT_SECRET"],
                "POSTGRES_PASSWORD": all_secrets["POSTGRES_PASSWORD"],
            },
        )
    )
else:
    k8s_yaml(
        secret_from_dict(
            "all-the-kas-secrets",
            inputs=only_secrets_named(
                "EAS_CERTIFICATE",
                "KAS_EC_SECP256R1_CERTIFICATE",
                "KAS_CERTIFICATE",
                "KAS_EC_SECP256R1_PRIVATE_KEY",
                "KAS_PRIVATE_KEY",
                "ca-cert.pem",
            ),
        )
    )
    k8s_yaml(
        secret_from_dict(
            "postgres-password",
            inputs=only_secrets_named("POSTGRES_PASSWORD"),
        )
    )


#   o8o
#   `"'
#  oooo  ooo. .oo.  .oo.    .oooo.    .oooooooo  .ooooo.   .oooo.o
#  `888  `888P"Y88bP"Y88b  `P  )88b  888' `88b  d88' `88b d88(  "8
#   888   888   888   888   .oP"888  888   888  888ooo888 `"Y88b.
#   888   888   888   888  d8(  888  `88bod8P'  888    .o o.  )88b
#  o888o o888o o888o o888o `Y888""8o `8oooooo.  `Y8bod8P' 8""888P'
#                                    d"     YD
#                                    "Y88888P'
#

OPENTDF_ABACUS_YML = "tests/integration/frontend.yaml"

if "opentdf-abacus" in to_edit:
    OPENTDF_ABACUS_YML = "tests/integration/frontend-local.yaml"
    # frontend folder should be next to backend
    docker_build("opentdf/abacus", "../frontend")

if "opentdf-abacus-tdf3" in to_edit:
    OPENTDF_ABACUS_YML = "tests/integration/frontend-local.yaml"
    # frontend folder should be next to backend
    docker_build("opentdf/abacus", "../frontend/DockerfileTests")

docker_build(
    CONTAINER_REGISTRY + "/opentdf/python-base",
    context="containers/python_base",
    build_args={
        "ALPINE_VERSION": ALPINE_VERSION,
        "CONTAINER_REGISTRY": CONTAINER_REGISTRY,
        "PY_VERSION": PY_VERSION,
    },
)

docker_build(
    CONTAINER_REGISTRY + "/opentdf/keycloak-multiarch-base",
    "./containers/keycloak-protocol-mapper/keycloak-containers/server",
    build_args={
        "CONTAINER_REGISTRY": CONTAINER_REGISTRY,
    },
)

docker_build(
    CONTAINER_REGISTRY + "/opentdf/keycloak",
    context="./containers/keycloak-protocol-mapper",
    build_args={
        "CONTAINER_REGISTRY": CONTAINER_REGISTRY,
        "KEYCLOAK_BASE_IMAGE": CONTAINER_REGISTRY + "/opentdf/keycloak-multiarch-base",
        "KEYCLOAK_BASE_VERSION": KEYCLOAK_BASE_VERSION,
        "MAVEN_VERSION": "3.8.4",
        "JDK_VERSION": "11",
    },
)

docker_build(
    CONTAINER_REGISTRY + "/opentdf/kas",
    build_args={
        "ALPINE_VERSION": ALPINE_VERSION,
        "CONTAINER_REGISTRY": CONTAINER_REGISTRY,
        "PY_VERSION": PY_VERSION,
        "PYTHON_BASE_IMAGE_SELECTOR": "",
    },
    context="containers/kas",
    live_update=[
        sync("./containers/kas", "/app"),
        run(
            "cd /app && pip install -r requirements.txt",
            trigger="./containers/kas/requirements.txt",
        ),
    ],
)

for microservice in ["attributes", "entitlements", "claims"]:
    image_name = CONTAINER_REGISTRY + "/opentdf/" + microservice
    docker_build(
        image_name,
        build_args={
            "ALPINE_VERSION": ALPINE_VERSION,
            "CONTAINER_REGISTRY": CONTAINER_REGISTRY,
            "PY_VERSION": PY_VERSION,
            "PYTHON_BASE_IMAGE_SELECTOR": "",
        },
        container_args=["--reload"],
        context="containers",
        dockerfile="./containers/" + microservice + "/Dockerfile",
        live_update=[
            sync("./containers/python_base", "/app/python_base"),
            sync("./containers/" + microservice, "/app/" + microservice),
            run(
                "cd /app/ && pip install -r requirements.txt",
                trigger="./containers/" + microservice + "/requirements.txt",
            ),
        ],
    )

# remote resources
# usage https://github.com/tilt-dev/tilt-extensions/tree/master/helm_remote#additional-parameters

postgres_helm_values = "deployments/docker-desktop/tdf-postgresql-values.yaml"
keycloak_helm_values = "deployments/docker-desktop/keycloak-values.yaml"

if isCI:
    postgres_helm_values = "tests/integration/backend-postgresql-values.yaml"
    keycloak_helm_values = "tests/integration/backend-keycloak-values.yaml"

helm_remote(
    "keycloak",
    version="17.0.1",
    repo_url="https://codecentric.github.io/helm-charts",
    values=[keycloak_helm_values],
)

helm_remote(
    "postgresql",
    repo_url="https://charts.bitnami.com/bitnami",
    release_name="opentdf",
    version="10.16.2",
    values=[postgres_helm_values],
)

#                                           o8o
#                                           `"'
#   .oooo.o  .ooooo.  oooo d8b oooo    ooo oooo   .ooooo.   .ooooo.   .oooo.o
#  d88(  "8 d88' `88b `888""8P  `88.  .8'  `888  d88' `"Y8 d88' `88b d88(  "8
#  `"Y88b.  888ooo888  888       `88..8'    888  888       888ooo888 `"Y88b.
#  o.  )88b 888    .o  888        `888'     888  888   .o8 888    .o o.  )88b
#  8""888P' `Y8bod8P' d888b        `8'     o888o `Y8bod8P' `Y8bod8P' 8""888P'
#
# usage https://docs.tilt.dev/helm.html#helm-options

opentdf_attrs_values = "deployments/docker-desktop/attributes-values.yaml"
opentdf_attrs_set = [
    "image.name=" + CONTAINER_REGISTRY + "/opentdf/attributes",
    "secretRef.name=postgres-password",
]
opentdf_claims_values = "deployments/docker-desktop/claims-values.yaml"
opentdf_claims_set = [
    "image.name=" + CONTAINER_REGISTRY + "/opentdf/claims",
    "secretRef.name=postgres-password",
]
opentdf_entitlements_values = "deployments/docker-desktop/entitlements-values.yaml"
opentdf_entitlements_set = [
    "image.name=" + CONTAINER_REGISTRY + "/opentdf/entitlements",
    "secretRef.name=postgres-password",
]
opentdf_kas_values = "deployments/docker-desktop/kas-values.yaml"
opentdf_kas_set = [
    "image.name=" + CONTAINER_REGISTRY + "/opentdf/kas",
    "secretRef.name=all-the-kas-secrets",
    "certFileSecretName=all-the-kas-secrets",
]

if isCI:
    opentdf_attrs_values = "tests/integration/backend-attributes-values.yaml"
    opentdf_attrs_set = ["image.name=" + CONTAINER_REGISTRY + "/opentdf/attributes"]
    opentdf_claims_values = "tests/integration/backend-claims-values.yaml"
    opentdf_claims_set = ["image.name=" + CONTAINER_REGISTRY + "/opentdf/claims"]
    opentdf_entitlements_values = "tests/integration/backend-entitlements-values.yaml"
    opentdf_entitlements_set = [
        "image.name=" + CONTAINER_REGISTRY + "/opentdf/entitlements"
    ]
    opentdf_kas_values = "tests/integration/backend-kas-values.yaml"
    opentdf_kas_set = ["image.name=" + CONTAINER_REGISTRY + "/opentdf/kas"]

k8s_yaml(
    helm(
        "charts/attributes",
        "opentdf-attributes",
        set=opentdf_attrs_set,
        values=[opentdf_attrs_values],
    )
)

k8s_yaml(
    helm(
        "charts/claims",
        "opentdf-claims",
        set=opentdf_claims_set,
        values=[opentdf_claims_values],
    )
)

k8s_yaml(
    helm(
        "charts/entitlements",
        "opentdf-entitlements",
        set=opentdf_entitlements_set,
        values=[opentdf_entitlements_values],
    )
)

k8s_yaml(
    helm(
        "charts/kas",
        "opentdf-kas",
        set=opentdf_kas_set,
        values=[opentdf_kas_values],
    )
)

k8s_yaml(
    helm(
        "charts/keycloak_bootstrap",
        "keycloak-bootstrap",
        set=["image.name=" + CONTAINER_REGISTRY + "/opentdf/keycloak-bootstrap"],
        values=["tests/integration/backend-keycloak-bootstrap-values.yaml"],
    )
)
k8s_yaml("tests/integration/ingress-class.yaml")
k8s_yaml(OPENTDF_ABACUS_YML)

# TODO this service requires actual S3 secrets
# TODO or use https://github.com/localstack/localstack
# k8s_yaml(
#     secret_from_dict(
#         "tdf-storage-secrets",
#         inputs={
#             "AWS_SECRET_ACCESS_KEY": "mySecretAccessKey",
#             "AWS_ACCESS_KEY_ID": "myAccessKeyId",
#         },
#     )
# )
# docker_build(
#     CONTAINER_REGISTRY + "/opentdf/storage",
#     context="containers/storage",
#     build_args={
#         "ALPINE_VERSION": ALPINE_VERSION,
#         "CONTAINER_REGISTRY": CONTAINER_REGISTRY,
#         "PY_VERSION": PY_VERSION,
#         "PYTHON_BASE_IMAGE_SELECTOR": "",
#     },
# )
# k8s_yaml(helm('charts/storage', 'storage', values=['deployments/docker-desktop/storage-values.yaml']))
# deprecated
# k8s_yaml(helm('charts/eas', 'eas', values=['deployments/docker-desktop/eas-values.yaml']))

# resource dependencies
k8s_resource("opentdf-attributes", resource_deps=["opentdf-postgresql"])
k8s_resource("opentdf-claims", resource_deps=["opentdf-postgresql", "keycloak"])
k8s_resource("opentdf-entitlements", resource_deps=["opentdf-postgresql"])
k8s_resource("opentdf-kas", resource_deps=["opentdf-attributes"])

#     o8o
#     `"'
#    oooo  ooo. .oo.    .oooooooo oooo d8b  .ooooo.   .oooo.o  .oooo.o
#    `888  `888P"Y88b  888' `88b  `888""8P d88' `88b d88(  "8 d88(  "8
#     888   888   888  888   888   888     888ooo888 `"Y88b.  `"Y88b.
#     888   888   888  `88bod8P'   888     888    .o o.  )88b o.  )88b
#    o888o o888o o888o `8oooooo.  d888b    `Y8bod8P' 8""888P' 8""888P'
#                      d"     YD
#                      "Y88888P'
#
# TODO should integrate with a service mesh and stop deploying our own ingress
# We need to have big headers for the huge bearer tokens we pass around
# https://kubernetes.github.io/ingress-nginx/user-guide/nginx-configuration/configmap/

helm_remote(
    "ingress-nginx",
    repo_url="https://kubernetes.github.io/ingress-nginx",
    set=["controller.config.large-client-header-buffers=20 32k"],
    version="4.0.16",
)

k8s_resource("ingress-nginx-controller", port_forwards="65432:80")

#     .o8                               .                .
#    "888                             .o8              .o8
#     888oooo.   .ooooo.   .ooooo.  .o888oo  .oooo.o .o888oo oooo d8b  .oooo.   oo.ooooo.
#     d88' `88b d88' `88b d88' `88b   888   d88(  "8   888   `888""8P `P  )88b   888' `88b
#     888   888 888   888 888   888   888   `"Y88b.    888    888      .oP"888   888   888
#     888   888 888   888 888   888   888 . o.  )88b   888 .  888     d8(  888   888   888
#     `Y8bod8P' `Y8bod8P' `Y8bod8P'   "888" 8""888P'   "888" d888b    `Y888""8o  888bod8P'
#                                                                                888
#                                                                               o888o
#

docker_build(
    "ghcr.io/opentdf/keycloak-bootstrap",
    "./containers/keycloak-bootstrap",
    build_args={"ALPINE_VERSION": ALPINE_VERSION, "PY_VERSION": PY_VERSION},
)

k8s_resource(
    "keycloak",
    links=[link("localhost:65432/auth", "Keycloak admin console")],
)

k8s_resource("keycloak-bootstrap", resource_deps=["keycloak", "opentdf-entitlements"])

#    db    db d888888b d88888b .d8888. d888888b
#    `8b  d8' `~~88~~' 88'     88'  YP `~~88~~'
#     `8bd8'     88    88ooooo `8bo.      88
#     .dPYb.     88    88~~~~~   `Y8b.    88
#    .8P  Y8.    88    88.     db   8D    88
#    YP    YP    YP    Y88888P `8888Y'    YP

docker_build(
    "opentdf/tests-clients",
    context="./",
    dockerfile="./tests/containers/clients/Dockerfile",
    # todo: (PLAT-1650) Force to x86 mode until we have a python built in arch64
    platform="linux/amd64",
)

k8s_yaml("tests/integration/xtest.yaml")

k8s_resource(
    "opentdf-xtest",
    resource_deps=["keycloak-bootstrap", "keycloak", "opentdf-kas", "opentdf-claims"],
)

# The Postgres chart by default does not remove its Persistent Volume Claims: https://github.com/bitnami/charts/tree/master/bitnami/postgresql#uninstalling-the-chart
# This means `tilt down && tilt up` will leave behind old PGSQL databases and volumes, causing weirdness.
# Doing a `tilt down && kubectl delete pvc --all` will solve this
# Tried to automate that teardown postcommand here with Tilt, and it works for everything but `tilt ci` which keeps
# waiting for the no-op `apply_cmd` to stream logs as a K8S resource.
# I have not figured out a clean way to run `down commands` with tilt
# k8s_custom_deploy("Manual PVC Delete On Teardown", 'echo ""', "kubectl delete pvc --all", "")
